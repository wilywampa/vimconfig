#!/usr/bin/env zsh
if [ -z $TMUX ]; then
  echo "Not in tmux!"
  exit 1
fi

local fmt='#{pane_current_command},,#{pane_id}'

# Read from stdin when used from copy-pane in tmux
[ -t 0 ] || read stdin

# Check if tab diff is requested with findvim -d file1 file2
[[ $# == 3 ]] && [[ "$1" == "-d" ]] && dodiff=1 || dodiff=0

remote() {
  local line=
  local file="$1"
  if [[ "$1" =~ ':([0-9]+)$' ]]; then
    line=$match
    [[ "$1" =~ '^(.*):' ]] && file="$match"
  elif [[ "$1" =~ '\(([0-9]+)\)$' ]]; then
    line=$match
    [[ "$1" =~ '^(.*)\(' ]] && file="$match"
  fi
  if [[ -d $file ]]; then
    vim --servername ${VIMSERVER:-VIM} --remote-send \
      '<C-\><C-n>:edit '"${(q)file:A}"'<CR>'
  elif [[ -n $line ]]; then
    vim --servername ${VIMSERVER:-VIM} --remote +"${line}|sil! foldo!" "$file"
  else
    vim --servername ${VIMSERVER:-VIM} --remote "$file"
  fi
}

vim --serverlist | grep -q "\b${VIMSERVER:-VIM}\b" 2>&1 > /dev/null
if [ $? -eq 0 ]; then
  if [[ $# == 0 ]] && [[ -n "$stdin" ]]; then
    panepwd=$(tmux display-message -p "#{pane_current_path}")
    stdin="${${${${stdin#\'}%\'}#\"}%\"}"
    if [[ ${(e)~stdin} != ./* && -e ${${(e)~stdin}%%[:(]*} ]]; then
      remote "${(e)~stdin}"
    elif [[ -d "$panepwd" ]] && [[ -e "$panepwd"/"${${${stdin#./}#/}%%[:(]*}" ]]; then
      remote "$panepwd"/"${${stdin#./}#/}"
    else
      tmux display-message "${stdin} does not exist"
    fi
  elif [[ $dodiff == 0 ]]; then
    remote "$@"
  fi
  for win in $(tmux list-windows -F '#{window_id}'); do
    for pane in $(tmux list-panes -t$win -F "$fmt"); do
      command=$pane[(ws:,,:)1]
      pane_id=$pane[(ws:,,:)2]
      if [[ "$command" = 'vim' ]]; then
        tmux select-window -t$win
        if [[ $(tmux display-message -p '#{pane_id}') != $pane_id ]]; then
          tmux select-pane -t$pane_id
        fi
        if [[ $dodiff == 1 ]]; then
          file1=$(readlink -f "$2")
          file2=$(readlink -f "$3")
          tmux send-keys Escape Escape ':tabe ' "${file1// /\\ }" 'Enter'
          tmux send-keys Escape Escape ':vert diffs ' "${file2// /\\ }" 'Enter'
        fi
        exit 0
      fi
    done
  done
fi

exec vim --servername ${VIMSERVER:-VIM} "$@"
exit 0
